import org.springframework.boot.gradle.tasks.run.BootRun

plugins {
    id "java-library"
    id "org.springframework.boot" version "3.1.9"
    id "io.spring.dependency-management" version "1.1.4"
}

java {
    toolchain { languageVersion.set(JavaLanguageVersion.of(17)) }
}

bootRun {
    mainClass = "dev.gemfire.expiration.custom.ExpirationClient"
}

tasks.register("bootRunEntity", BootRun) {
    mainClass = "dev.gemfire.expiration.entity.EntityDefinedExpirationClient"
    classpath = sourceSets.main.runtimeClasspath
}

repositories {
    mavenCentral()
    maven {
        credentials {
            username "$gemfireRepoUsername"
            password "$gemfireRepoPassword"
        }
        url = uri("https://commercial-repo.pivotal.io/data3/gemfire-release-repo/gemfire")
    }
}

dependencies {
    implementation "com.vmware.gemfire:spring-boot-3.1-gemfire-10.1:1.0.0"
    implementation "com.vmware.gemfire:gemfire-core:10.1.0"
    implementation "org.awaitility:awaitility:4.2.0"
}
